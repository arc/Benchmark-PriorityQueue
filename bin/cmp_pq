#! /usr/bin/perl

use v5.10;
use strict;
use warnings;

use Benchmark qw<cmpthese>;
use Benchmark::PriorityQueue qw<all_backends all_tasks make_shim>;
use Getopt::Long qw<GetOptions :config gnu_getopt>;

my $USAGE = <<'EOF';
Usage: cmp_pq [OPTION]...

Options:

  -i ITERATIONS
    Number of iterations to run in each timing test; default 10; negative
    means number of seconds to iterate

  -r RANK
    Integer indicating the size of each workload; multiple allowed; 1000 if
    none specified

  -b BACKEND
    Backend module to benchmark, like List::Priority; multiple allowed; all
    if none specified

  -t TASK
    Task to run; multiple allowed; all if none specified

EOF

GetOptions(\my %opt, qw<i=i r=i@ b=s@ t=s@ help|?>)
    or die $USAGE;

if ($opt{help}) {
    print $USAGE;
    exit 0;
}

$opt{i} //= 10;
$opt{r} ||= [1000];
$opt{b} ||= [all_backends()];
$opt{t} ||= [all_tasks()];

my %abbr;
{
    my %seen;
    for my $backend (@{ $opt{b} }) {
        (my $abbr = $backend)
            =~ s/ (?<= [A-Z] ) [A-Z]* (?: (?=::) | [_a-z0-9]+ ) //xmsg;
        $abbr =~ s/::/:/g;
        my $disambiguated = $abbr;
        for (my $n = 1; ; $disambiguated = sprintf '%s-%d', $abbr, ++$n) {
            if (!$seen{$disambiguated}++) {
                say "$disambiguated = $backend";
                $abbr{$backend} = $disambiguated;
                last;
            }
        }
    }
}

for my $task (@{ $opt{t} }) {
    my @shims =
        grep { $_->supports($task) }
        map  { make_shim($_, iterations => $opt{i}) }
        @{ $opt{b} };
    for my $rank (@{ $opt{r} }) {
        local $| = 1;
        say '';
        print "Running $task on ", scalar @shims, " backends, rank=$rank ";
        my %result;
        for my $shim (@shims) {
            print '.';
            my $abbr = $abbr{ $shim->backend };
            $result{$abbr} = $shim->time_workload($task, $rank);
        }
        say '';
        cmpthese(\%result);
    }
}
